Option Explicit

Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swPart As SldWorks.PartDoc

Dim xlApp As Excel.Application


Function Export(strDWGFile) As Boolean

Dim dblAlignment(11) As Double

dblAlignment(0) = 0
dblAlignment(1) = 0
dblAlignment(2) = 0
dblAlignment(3) = 1
dblAlignment(4) = 0
dblAlignment(5) = 0
dblAlignment(6) = 0
dblAlignment(7) = 1
dblAlignment(8) = 0
dblAlignment(9) = 0
dblAlignment(10) = 0
dblAlignment(11) = 1


Export = swPart.ExportToDWG2(strDWGFile, swModel.GetPathName, SwConst.swExportToDWG_e.swExportToDWG_ExportSelectedFacesOrLoops, True, dblAlignment, False, False, 0, Null)


End Function

Function GetExportFolder(strWorkingFolder As String) As String

Dim ofFileDialog As Office.FileDialog

Dim strExportFolder As String

strExportFolder = GetSetting("Solidworks Macros", "ExportToDWG", "ExportFolder")
If strExportFolder = "" Then
    strExportFolder = strWorkingFolder
End If

Set ofFileDialog = xlApp.FileDialog(msoFileDialogFolderPicker)
ofFileDialog.InitialFileName = strExportFolder
ofFileDialog.Title = "Export to ..."
If ofFileDialog.Show = -1 Then
    GetExportFolder = CStr(ofFileDialog.SelectedItems(1))
Else
    GetExportFolder = strExportFolder
End If

Call SaveSetting("Solidworks Macros", "ExportToDWG", "ExportFolder", GetExportFolder)
    
Set ofFileDialog = Nothing

End Function

Sub main()


Dim swModelExtension As SldWorks.ModelDocExtension
Dim swSelectionManager As SldWorks.SelectionMgr

Dim strDirectory As String
Dim strName As String
Dim strMaterialDetails As String
Dim strMaterialDetailsArray() As String
Dim strMaterial As String
Dim dblThickness As Double
Dim strDWGFile As String
Dim strExcelFile As String
Dim bolExport As Boolean

Set swApp = Application.SldWorks
Set swModel = swApp.ActiveDoc()
Set swModelExtension = swModel.Extension()
Set swPart = swModel
Set swSelectionManager = swModel.SelectionManager()

If swSelectionManager.GetSelectedObjectCount2(0) = 1 And swSelectionManager.GetSelectedObjectType3(1, 0) = SwConst.swSelectType_e.swSelFACES Then
    If swModel.LengthUnit = SwConst.swLengthUnit_e.swINCHES Then
        Call swApp.RunCommand(SwCommands.swCommands_NormalTo, "")
    
        strDirectory = swApp.GetCurrentWorkingDirectory()
        strName = InputBox("Name?", , swModel.GetTitle())
    
        strMaterialDetails = swPart.MaterialIdName()
        strMaterialDetailsArray = Split(strMaterialDetails, "|")
        strMaterial = strMaterialDetailsArray(1)
    
        dblThickness = FindThickness()
            
        Set xlApp = CreateObject("Excel.Application")
        strDirectory = GetExportFolder(swApp.GetCurrentWorkingDirectory())
    
        strDWGFile = strDirectory & "\" & strName & ".dwg"
        bolExport = Export(strDWGFile)
        If bolExport Then
            strExcelFile = CheckExcelFileExist(strDirectory)
            Call UpdateExcel(strExcelFile, strName, strMaterial, dblThickness)
            Set xlApp = Nothing
            
            Call MsgBox("Export was successful", vbOKOnly, "Done")
        End If
    Else
        Call MsgBox("Change units to inch and try again.", vbOKOnly, "Error")
    End If
Else
    Call MsgBox("Select only one face and try again.", vbOKOnly, "Error")
End If

End Sub


Function CheckExcelFileExist(strDirectory As String) As String

Dim strFileName As String
Dim strSourceFile As String

strFileName = strDirectory & "\Part List.xlsx"
strSourceFile = "C:\Users\farzad\Desktop\Macros\Part List.xlsx"

If Dir(strFileName) = "" Then
    Call FileCopy(strSourceFile, strFileName)
End If

CheckExcelFileExist = strFileName

End Function




Function FindThickness() As Double

Dim swFeature As SldWorks.Feature
Dim swExtrude As SldWorks.ExtrudeFeatureData2
Dim swSheetMetal As SldWorks.SheetMetalFeatureData

Dim strType As String

Set swFeature = swPart.FirstFeature

Do While Not swFeature Is Nothing
    Debug.Print (swFeature.Name & " : " & swFeature.GetTypeName2)
    
    strType = swFeature.GetTypeName2
     
    If swFeature.GetTypeName2 = "Extrusion" Then
        Set swExtrude = swFeature.GetDefinition()
        FindThickness = (swExtrude.GetDepth(True) + swExtrude.GetDepth(False)) / 0.0254
        
        Set swFeature = Nothing
        Set swExtrude = Nothing
        
        Exit Function
    ElseIf swFeature.GetTypeName2 = "SheetMetal" Then
        Set swSheetMetal = swFeature.GetDefinition()
        FindThickness = swSheetMetal.Thickness() / 0.0254
        
        Set swFeature = Nothing
        Set swSheetMetal = Nothing
        
        Exit Function
    End If
    
    Set swFeature = swFeature.GetNextFeature
Loop

Set swFeature = Nothing

FindThickness = 0

End Function
Sub UpdateExcel(strFilePath As String, strName As String, strMaterial As String, dblThickness As Double)

Dim xlWorkbook As Excel.Workbook
Dim xlWorksheet As Excel.Worksheet
Dim xlCell As Excel.Range

Dim lngRow As Long

Set xlWorkbook = xlApp.Workbooks.Open(strFilePath)
Set xlWorksheet = xlWorkbook.Worksheets("Sheet1")

For Each xlCell In xlWorksheet.Columns(1).Cells
    If IsEmpty(xlCell) Then
        lngRow = xlCell.Row
        Exit For
    End If
Next

xlWorksheet.Cells(lngRow, 1) = lngRow - 1
xlWorksheet.Cells(lngRow, 2) = strName
xlWorksheet.Cells(lngRow, 3) = InputBox("How many?", strName, 1)
xlWorksheet.Cells(lngRow, 4) = strMaterial
xlWorksheet.Cells(lngRow, 5) = Format(dblThickness, "#0.0000") & Chr(34)
xlWorksheet.Cells(lngRow, 6) = InputBox("Procedure?", strName, "Laser")
xlWorksheet.Cells(lngRow, 7) = InputBox("Bend?", strName, "")
xlWorksheet.Cells(lngRow, 8) = InputBox("Our material?", strName, "")
xlWorksheet.Cells(lngRow, 9) = InputBox("Description?", strName, "Painted-Ali")

Call xlWorkbook.Save
Call xlWorkbook.Close

Set xlWorkbook = Nothing
Set xlWorksheet = Nothing
Set xlCell = Nothing


End Sub


